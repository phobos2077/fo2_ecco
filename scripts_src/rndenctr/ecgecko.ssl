/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/* Random Encounter scripts. These will be filled in more once they
   have been designed.
   
   updated:
   - phobos2077: gecko seek food (trapping)
*/

#define SCRIPT_REALNAME "ecgecko"
#include "../headers/define.h"
#include "../headers/bhrnddst.h"

#define NAME                    SCRIPT_ECGECKO

#include "../headers/command.h"


#include "..\_pbs_headers\gecko_hunger.h" // phobos2077

procedure start;
procedure destroy_p_proc;
procedure critter_p_proc;
procedure map_enter_p_proc;
procedure combat_p_proc; // phobos2077

variable starttile := 0;

procedure map_enter_p_proc begin
   starttile := self_tile;
   set_self_team(TEAM_RND_SILVER_GECKO);
   reg_anim_clear(self_obj);
   reg_anim_begin();
   self_rotate(Random(0, 5));
   reg_anim_end();
end

procedure start begin
end

procedure critter_p_proc begin
   if (self_can_see_dude) then begin
      attack(dude_obj);
   end

   if (not anim_busy(self_obj) and not food) then begin // pbs
      reg_anim_clear(self_obj);
      reg_anim_begin();
      self_walk_to_tile(tile_num_in_direction(starttile, Random(0, 5), Random(3, 10)));
      reg_anim_end();
   end
   
   if (not(combat_is_initialized)) then SEEK_AND_EAT_FOOD_GECKO // phobos2077
end

// proc added by phobos2077:
procedure combat_p_proc begin
   call forgetFood;
end

procedure destroy_p_proc begin
   variable tail:=0;

   //if ((obj_pid(self_obj) == PID_GECKO_SMALL) or (obj_pid(self_obj) == PID_TOUGH_GECKO_SMALL)) then
   //     item := create_object(PID_GECKO_PELT,0,0);
   //else
   //     item := create_object(PID_GOLDEN_GECKO_PELT,0,0);

   //added by killap
   if (cur_map_index == MAP_BH_RND_DESERT) then begin
       dec_map_var(MVAR_Hostile_Total);
   end
   //end added by killap

   if (dude_perk(PERK_gecko_skinning_perk)) then begin
       if ((self_pid == PID_GECKO_SMALL) or (self_pid == PID_TOUGH_GECKO_SMALL)) then
            tail := create_object(PID_GECKO_PELT,0,0);
       // Begin Seraph's Code
       else if ((self_pid == PID_FIRE_GECKO) or (self_pid == PID_TOUGH_FIRE_GECKO)) then
            tail := create_object(PID_FIRE_GECKO_PELT,0,0);
       // End Seraph's Code
       else if ((self_pid == PID_GOLDEN_GECKO) or (self_pid == PID_TOUGH_GOLDEN_GECKO)) then
            tail := create_object(PID_GOLDEN_GECKO_PELT,0,0);

      add_obj_to_inven(self_obj,tail);
   end
end