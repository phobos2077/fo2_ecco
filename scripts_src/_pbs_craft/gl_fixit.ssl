procedure start;
procedure create_button;
procedure delete_button;

#include "..\sfall\sfall.h"
#include "..\sfall\define_lite.h"
#include "..\sfall\lib.misc.h"
#include "..\sfall\dik.h"

//#define PID_BLOCKING_HEX             (33554499)
//#define PID_WORKBENCH                (157)
//#define SCRIPT_TEST0                 (2)

#include "mrfixit.h"



variable ini_fixit_hotkey;
variable warning_msg, batch_scheduled, fixit_button_shown, screen_center_x;

#define WIN_CRAFT_BTN         "helwin"
#define SFX_BUTTON_DOWN       "butin4"
#define SFX_BUTTON_UP         "IB2LU1X1"
#define SFX_ERROR             "IISXXXX1"
#define PCX_BUTTON_DOWN       "PCX/mfdown.pcx"
#define PCX_BUTTON_UP         "PCX/mfup.pcx"


procedure warning_combat begin
   if (warning_msg == false) then begin
      warning_msg := true;
      play_sfx(SFX_ERROR);
      sfall_func2("message_box", bstr(121), MSGBOX_NORMAL);
      warning_msg := false;
   end
end


procedure keypress_handler begin
   variable pressed := get_sfall_arg,
            key := get_sfall_arg,
            optionProc,
            oldMode;
   if (pressed) then begin
      if (hotkey_pressed_now(ini_fixit_hotkey, key)) then begin
         if combat_is_initialized then
            call warning_combat;
         else if (not(get_game_mode) and mode <= 0) then begin
            call batch_init;
      	end
      end else if (mode > 0) then begin
         oldMode := mode;
         if (key == DIK_ESCAPE) then begin
            call exit_mode;
         end else if (key >= DIK_1 and key <= DIK_9) then begin
            optionProc := "item_options_" + (key - DIK_1);
            debug_msg("select "+optionProc); // this debug is needed to prevent stupid optimizer from removing the string!
            call optionProc;
         end else if (key == DIK_0 and mode <= 4) then begin
            if (mode == 2) then
               call items_list_mode;
            else if (mode == 1 and use_categories) then
               call item_categories_mode;
            else if (mode >= 3) then
               call item_options_mode;
         end else if (key == DIK_COMMA and mode == 1 and has_prev_page) then begin
            call list_back_proc;
         end else if (key == DIK_PERIOD and mode == 1 and has_next_page) then begin
            call list_next_proc;
         end
         if (mode != oldMode) then
            tap_key(DIK_ESCAPE);
      end
   end
end

// Player has the cursor on the button?
procedure do_on begin
   mouse_over := true;
end

procedure do_off begin
   if (button_pressed) then play_sfx(SFX_BUTTON_UP);
   mouse_over := false;
   button_pressed := false;
end

procedure do_down begin
   if (button_pressed == false) then begin
      button_pressed := true;
      play_sfx(SFX_BUTTON_DOWN);
   end
end

procedure do_up begin
   button_pressed := false;

   if combat_is_initialized then begin
      play_sfx(SFX_BUTTON_UP);
      call warning_combat;
   end
   else if (not(get_game_mode) and mode <= 0) then begin
      //set_sfall_global("openfixi", 1);
      play_sfx(SFX_BUTTON_UP);
      //call batch_init in 1;
      batch_scheduled := true;
      set_global_script_repeat(5);
   end
end

// Create the button
procedure create_button begin
   if fixit_button_shown then return;

   create_win_flag(WIN_CRAFT_BTN, (screen_center_x + 237), (get_screen_height - 98), 20, 18, WIN_FLAG_DONTMOVE /*bwor WIN_FLAG_TRANSPARENT*/);
   SelectWin(WIN_CRAFT_BTN);
   AddButton("but", 0, 0, 20, 18);
   AddButtonGFX("but", PCX_BUTTON_DOWN, PCX_BUTTON_UP, PCX_BUTTON_UP);
   AddButtonProc("but", do_on, do_off, do_down, do_up);
   ShowWin;
   fixit_button_shown := true;
end

procedure delete_button begin
   if not fixit_button_shown then return;
   
   fixit_button_shown := false;
   mouse_over := 0;
   DeleteWin(WIN_CRAFT_BTN);
end

procedure gamemodechange_handler begin
   variable gameExit := get_sfall_arg;
   if (not (get_game_mode bwand LOADGAME) and not gameExit) then begin
      call create_button;
   end
   else begin
      call delete_button;
   end
end

procedure start begin
	if (game_loaded) then begin
      //craft_msg := add_extra_msg_file("craft.msg");
		ini_fixit_hotkey := parse_hotkey(cfg_fixit_hotkey);
		register_hook_proc(HOOK_KEYPRESS, keypress_handler);
      register_hook_proc(HOOK_GAMEMODECHANGE, gamemodechange_handler);

      screen_center_x  := (get_screen_width / 2);
      if ((get_ini_setting("f2_res.ini|IFACE|IFACE_BAR_WIDTH") != 800) or (screen_center_x < 400)) then begin
         screen_center_x -= 80;
      end
      call create_button;

      set_global_script_type(1);
	end else begin
      //debug_msg("gl_fixit repeats");
      if (batch_scheduled) then begin
         batch_scheduled := false;
         call batch_init;
      end
      set_global_script_repeat(0);
   end
end

