/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/******************************************************************************************
        Item: Buckner Stock
        Locked: Yes
        Trapped: Yes
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: September 26, 1997

           Updated:
           - phobos2077: changed stock
******************************************************************************************/

/* Include Files */
#define SCRIPT_REALNAME "kibbox"
#include "../headers/define.h"
#define NAME                    SCRIPT_KIBBOX
#include "../headers/command.h"

/* Sets whether the door is locked or trapped */
#define LOCKED_STATUS                   STATE_ACTIVE
#define TRAPPED_STATUS                  STATE_INACTIVE
#define DOOR_STATUS                     STATE_METAL
#define LOCK_STATUS                     STATE_STANDARD_LOCK

#define LVAR_Restock_Time               (7)
#define LVAR_Restock_Time_Fix           (8) //added by killap

#define custom_start
#define custom_map_enter_p_proc
#define custom_map_update_p_proc
#include "../headers/containers.h"

import variable klam_bucknr_box_obj;

procedure start begin
   klam_bucknr_box_obj := self_obj;
end

/***************************************************************************************
   Whenever the map is first entered, this procedure will be called. The main purpose of
   this procedure is to lock the door from the outset, rather than having to worry about
   it through Mapper. After the door has been unlocked, it will remain as such, unless
   the player locks it once more.
***************************************************************************************/
procedure map_enter_p_proc begin
   /* Set up the door state when the player first enters the map */
   klam_bucknr_box_obj := self_obj;
   if (local_var(LVAR_Set_Door_Status) == 0) then begin
       set_local_var(LVAR_Set_Door_Status,1);
       set_local_var(LVAR_Locked,LOCKED_STATUS);
       set_local_var(LVAR_Trapped,TRAPPED_STATUS);
   end

   restock_fix
   if (local_var(LVAR_Restock_Time) < game_time) then begin
      variable tmp_box := move_critical_items();
      check_restock_item(PID_BOTTLE_CAPS, 150, 225, 100)
      check_restock_item(PID_LEATHER_ARMOR, 1, 1, 50) // pbs: probability halfed
      check_restock_item(PID_HEALING_POWDER, 2, 4, 100)
      check_restock_item(PID_MEAT_JERKY, 2, 4, 100)
      check_restock_item(PID_GECKO_PELT, 3, 6, 100)
      check_restock_item(PID_ROPE, 1, 3, 100) // pbs: raised max
      check_restock_item(PID_NUKA_COLA, 6, 12, 100)
      check_restock_item(PID_BEER, 6, 12, 100)
      check_restock_item(PID_BOOZE, 2, 4, 100)
      check_restock_item(PID_KNIFE, 2, 4, 100) // pbs: raised
      check_restock_item(PID_THROWING_KNIFE, 2, 4, 100)
      check_restock_item(PID_COMBAT_KNIFE, 0, 1, 50)
      check_restock_item(PID_CROWBAR, 1, 1, 100)
      check_restock_item(PID_STIMPAK, 1, 3, 100)
      check_restock_item(PID_FIRST_AID_KIT, 0, 1, 100)
      check_restock_item(PID_SPEAR, 2, 4, 100) // pbs: raised
      check_restock_item(PID_SHARPENED_POLE, 2, 6, 100) // pbs: added
      check_restock_item(PID_SPRINGER_RIFLE, 1, 1, 50) // pbs: added
      check_restock_item(PID_FLARE, 1, 3, 100)
      call restore_critical_items(tmp_box);
      set_local_var(LVAR_Restock_Time, (2 * ONE_GAME_DAY) + game_time);
   end

   if (local_var(LVAR_Locked) == STATE_ACTIVE) then begin
       obj_lock(self_obj);
   end

   else begin
       obj_unlock(self_obj);
   end
end

/**************************************************************************************
   This procedure gets called roughly every 30 seconds of real time. It is used to make
   sure that the door does not lock on it's own and that the player will be able to get
   through once he has unlocked the door. Once more, this is so that locking and
   unlocking do not need to be done in Mapper.
**************************************************************************************/

procedure map_update_p_proc begin
   klam_bucknr_box_obj := self_obj;
   if (local_var(LVAR_Locked) == STATE_ACTIVE) then begin
       obj_lock(self_obj);
   end

   else begin
       obj_unlock(self_obj);
   end
end
