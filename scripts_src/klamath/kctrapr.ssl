/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Generic Trapper for trapper town
        Location: Klamath
        Description:

        Note: Currently this script cannot be loaded by the scripting tool.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: Febuary 25, 1998

           Updated:
           - phobos2077: added dialogue to teach Gecko skinning and Leather Crafting
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "kctrapr"
#include "../headers/define.h"
#include "../headers/kladwtn.h"

#define NAME                    SCRIPT_KCTRAPR
#define TOWN_REP_VAR            (GVAR_TOWN_REP_KLAMATH)
#define floater(x)		float_msg(self_obj, mstr(x),FLOAT_MSG_NORMAL)
#define floater_angry(x)		float_msg(self_obj, mstr(x),FLOAT_MSG_RED)

#include "../headers/command.h"
#include "../headers/modreact.h"

#define Vault_Known             (1)     // Temp Until get command from Jesse
#define Visited_City            (1)     // Temp Until get command from Jesse

// by phobos2077:
#define Has_Skinner      (has_trait(TRAIT_PERK,dude_obj,PERK_gecko_skinning_perk))
#define Add_Skinner      critter_add_trait(dude_obj,TRAIT_PERK,PERK_gecko_skinning_perk,1)
#define TEACHER_MAP     (MAP_KLAMATH_MALL)
#define TEACHER_TILE    (26904)
#define TEACHER_ELEV    (0)
#define is_teacher_pos  (cur_map_index == TEACHER_MAP and self_tile == TEACHER_TILE and self_elevation == TEACHER_ELEV)
#define teacher_no      (1)
#define teacher_yes     (2)
#define teacher_taught  (3)
#define TEACHER_PRICE   (200)
#define CRAFT_PRICE     (200)


procedure NodeTeach1;
procedure NodeTeach2;
procedure NodeTeach3;
procedure NodeTeach4;

procedure NodeCraft1;
procedure NodeCraft2;
procedure NodeCraft3;
procedure NodeCraft4;
// end;

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure push_p_proc;

/* Script Specific Procedure Calls */
procedure Node910;
procedure Node920;
procedure Node995;
procedure Node996;
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Hostile                    (4)
#define LVAR_Personal_Enemy             (5)
#define LVAR_Teacher                    (6) // pbs

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable k;

// by phobos2077 - crafting
#include "../_pbs_craft/learn_craft.h"

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_KLAMATH);
   set_self_ai(AI_TOUGH_CITIZEN);
   // by phobos2077:
   if (local_var(LVAR_Teacher) == 0) then begin
      if (is_teacher_pos) then set_local_var(LVAR_Teacher, teacher_yes);
      else set_local_var(LVAR_Teacher, teacher_no);
   end
   // end by phobos;
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_global_var(GVAR_ENEMY_KLAMATH,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   GetReaction;
   
   if (local_var(LVAR_Teacher) >= teacher_yes and (not(Has_Skinner) or (get_sfall_global_int(SGVAR_CRAFT_JACKET) == 0))) then begin
      // new dialog
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call NodeTeach1;
      gSay_End;
      end_dialogue;
      return;
   end else if (local_var(LVAR_Teacher) == teacher_taught) then begin
      floater(random(520, 523));
      return;
   end

   if ((REP_ENEMY_KLAMATH) or (local_var(LVAR_Personal_Enemy) == 1)) then begin
       call Node998;
   end

   else if (global_var(GVAR_QUEST_RAT_GOD) == RAT_GOD_SUCCESS) then begin
      call Node004;
   end

   else if town_rep_worse_than(5) then begin
      call Node001;
   end

   else begin
      if (has_skill(dude_obj, SKILL_OUTDOORSMAN) > 30) then
         call Node003;
      else
         call Node002;
   end

end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (has_skill(dude_obj, SKILL_OUTDOORSMAN) > 15) then
      display_msg(mstr(random(103,105)));
   else
      display_msg(mstr(random(100,102)));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   if (has_skill(dude_obj, SKILL_OUTDOORSMAN) > 15) then
      display_msg(mstr(random(203,205)));
   else
      display_msg(mstr(random(200,202)));
end

procedure push_p_proc begin
   script_overrides;
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

procedure Node910 begin
end

procedure Node920 begin
//if player has drymeat subtract one piece and goto 2, else 3
end

procedure Node995 begin
   if (dude_charisma >= 7) then begin
       TopReact;
   end
   else begin
       BottomReact;
   end
end

procedure Node996 begin
   BottomReact;
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin //may want to make dog attack here
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end

procedure Node001 begin
   floater(random(110,126));
end

procedure Node002 begin
   floater(random(130,148));
end

procedure Node003 begin
   floater(random(150,162));
end

procedure Node004 begin
   floater(random(170,181));
end


// phobos2077 expansion - teach Skinning begin:
procedure NodeTeach1 begin
   Reply(500);
   
   if (not(Has_Skinner)) then begin
      NOption(501,NodeTeach2,004);
      NLowOption(502,NodeTeach2);
   end
   
   if (get_sfall_global_int(SGVAR_CRAFT_JACKET) == 0) then
      NOption(530,NodeCraft1,004);
   
   NOption(503,Node999,000);
end

procedure NodeTeach2 begin
   Reply(505);
   
   if (dude_caps > TEACHER_PRICE) then begin
      NOption(506,NodeTeach3,004);
      NLowOption(508,NodeTeach3);
   end
   NOption(507,NodeTeach4,004);
   NLowOption(509,NodeTeach4);
end

procedure NodeTeach3 begin
   Reply(510);
   Add_Skinner;
   set_local_var(LVAR_Teacher, teacher_taught);
   item_caps_adjust(dude_obj, -TEACHER_PRICE);
   NOption(511,Node999,004);
   NLowOption(512,Node999);
end

procedure NodeTeach4 begin
   NMessage(515);
end


procedure NodeCraft1 begin
   Reply(531);
   
   NOption(532,NodeCraft2,004);
end

procedure NodeCraft2 begin
   Reply(533);
   
   if (dude_caps >= CRAFT_PRICE) then
      NOption(534,NodeCraft3,004);
      
   NOption(535,Node999,004);
end

procedure NodeCraft3 begin
   Reply(537);
   
   set_sfall_global(SGVAR_CRAFT_JACKET, 1);
   display_msg(fixit_mstr(82));
   set_local_var(LVAR_Teacher, teacher_taught);
   item_caps_adjust(dude_obj, -CRAFT_PRICE);
   
   NOption(540, NodeCraft4,004);
end

procedure NodeCraft4 begin
   Reply(541);
   
   NOption(542, Node999,004);
end

// teach skinning end;