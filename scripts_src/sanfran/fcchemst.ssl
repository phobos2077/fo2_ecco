/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Chemist
        Location: San Francisco
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "fcchemst"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_FCCHEMST
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SAN_FRANCISCO)

#include "../headers/command.h"
#include "../headers/modreact.h"

#include "../headers/sanfran.h"


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending

// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure doCallGuards; // added by killap

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;
variable begin
   n2 := 0;
   callGuards := 0;
end

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_SAN_FRAN_SHI);
   set_self_ai(AI_SF_SHI);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   // added by killap
   if ((san_fran_flag(SF_SHI_ENEMY)) and (local_var(LVAR_Hostile) == 0)) then begin
      call Node017;
   // end added
   end else if ((local_var(LVAR_Hostile) != 0) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       self_attack_dude;
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (obj_in_party(source_obj)) then begin
       set_local_var(LVAR_Personal_Enemy,1);
       set_san_fran_flag(SF_SHI_ENEMY);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION2;

   CheckKarma;

   GetReaction;

   ReactToLevel; //added by killap

   if (san_fran_flag(SF_SHI_ENEMY)) then begin
      call Node017;
   end else if (armor_flag(ARMOR_START)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node018;
      gSay_End;
      end_dialogue;
   end else if ((armor_flag(ARMOR_KNOWN) or armor_flag(ARMOR_GOTTEN) or armor_flag(ARMOR_GOTTENPAID)) and dude_item(PID_HARDENED_POWER_ARMOR)) then begin //edit by killap - added ARMOR_GOTTENPAID
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node013;
      gSay_End;
      end_dialogue;

   //addded by killap
   end else if ( (armor_flag(ARMOR_GOTTEN)) or armor_flag(ARMOR_GOTTENPAID)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node018;
      gSay_End;
      end_dialogue;
   //end added


   end else if (armor_flag(ARMOR_SOLDFUELELRON) or armor_flag(ARMOR_SOLDFUELTANKER)) then begin
      call Node021;
   end else if (armor_flag(ARMOR_SOLDMONEY)) then begin
      call Node022;
   end else if (local_var(LVAR_Herebefore) > 0) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node020;
      gSay_End;
      end_dialogue;
   end else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end
   set_local_var(LVAR_Herebefore, 1);

   // added by killap
   if (callGuards) then begin
      callGuards := 0;
      call doCallGuards;
   end
   // end added
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node003a begin
   DownReactMinor;
   call Node003;
end

procedure Node005a begin
   DownReactMinor;
   call Node005;
end

procedure Node001 begin
   Reply(103);
   NLowOption(104, Node002);
   BOption(105, Node003a, 4);
   NOption(mstr(106) + " " + dude_name + mstr(171), Node004, 4);
   BOption(107, Node005a, 4);
   NOption(108, Node999, 4);
end

procedure Node002 begin
   if (n2 < 2) then begin
      Reply(109);
      NOption(111, Node999, 1);
   end else begin
      callGuards := 1;
      Reply(110);
      NOption(111, Node999, 1);
   end
   n2 += 1;
end

procedure NodeGuards begin
   callGuards := 1;
   call Node998; //edit by killap - was 999
end

procedure Node003 begin
   Reply(112);
   NOption(113, NodeGuards, 4);
   NOption(114, Node999, 4);
end

procedure Node96a begin
   if (san_fran_flag(SF_GAS_ELRONS)) then begin
      call Node009;
   end else begin
      call Node006;
   end
end

procedure Node108a begin
   if (tanker_flag(TANK_FUEL)) then
      call Node010;
   else
      call Node008;
end

procedure Node004 begin
   Reply(mstr(115) + " " + dude_name + mstr(172));
   if (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL)) then
      NOption(116, Node96a, 4);
   NOption(117, Node007, 4);
   if (GASBEGUN and not san_fran_flag(SF_GAS_ELRONS)) then
      NOption(118, Node108a, 4);
   NOption(119, Node999, 4);
end

procedure Node005 begin
   Reply(120);
   if (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL)) then
      NOption(121, Node96a, 4);
   NOption(122, Node007, 4);
   if (GASBEGUN and not san_fran_flag(SF_GAS_ELRONS)) then
      NOption(123, Node108a, 4);
   NOption(124, Node999, 4);
end

procedure Node011a begin
   SHOW_ARMOR_QUEST;
   set_armor_flag(ARMOR_START);
   call Node011;
end

procedure Node006 begin
   set_armor_flag(ARMOR_KNOWN);
   Reply(125);
   NOption(126, Node011a, 4);
   NOption(127, Node012, 4);
   if (dude_item(PID_HARDENED_POWER_ARMOR)) then
      NOption(128, Node013, 4);
end

procedure Node007 begin
   Reply(129);
   if (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL)) then
      NOption(130, Node96a, 4);
   if (GASBEGUN and not san_fran_flag(SF_GAS_ELRONS)) then
      NOption(131, Node108a, 4);
   NOption(132, Node999, 4);
end

procedure Node008 begin
   set_armor_flag(ARMOR_KNOWN);
   Reply(133);
   NOption(134, Node011a, 4);
   NOption(135, Node012, 4);
   if (dude_item(PID_HARDENED_POWER_ARMOR)) then
      NOption(136, Node013, 4);
end

procedure Node009 begin
   Reply(137);
   NOption(138, Node999, 4);
end

procedure Node010 begin
   Reply(139);
   NOption(140, Node999, 4);
end

procedure Node011 begin
   Reply(141);
   NOption(142, Node999, 4);
end

procedure Node012 begin
   Reply(143);
   NOption(144, Node999, 4);
end

procedure Node1014 begin
   variable obj;

   if (tanker_flag(TANK_FUEL)) then begin
      call Node010;
   end else begin
      COMPLETE_ELRON_GAS_QUEST;
      COMPLETE_ARMOR_QUEST;
      set_armor_flag(ARMOR_SOLDFUELELRON);
      obj := dude_item(PID_HARDENED_POWER_ARMOR);
      rm_obj_from_inven(dude_obj, obj);
      //add_obj_to_inven(self_obj, obj);
      destroy_object(obj); //added by killap
      call Node014;
   end
end

procedure Node0914 begin
   variable obj;

   if (san_fran_flag(SF_GAS_ELRONS)) then begin
      call Node009;
   end else begin
      COMPLETE_TANKER_FUEL;
      COMPLETE_ARMOR_QUEST;
      set_armor_flag(ARMOR_SOLDFUELTANKER);
      obj := dude_item(PID_HARDENED_POWER_ARMOR);
      rm_obj_from_inven(dude_obj, obj);
      destroy_object(obj);
      call Node014;
   end
end

// pbs: reduce money to match armor cost
#define POWER_ARMOR_PRICE     (12000) // was 20000

procedure Node015a begin
   variable obj;

   COMPLETE_ARMOR_QUEST;
   set_armor_flag(ARMOR_SOLDMONEY);
   obj := dude_item(PID_HARDENED_POWER_ARMOR);
   rm_obj_from_inven(dude_obj, obj);
   //add_obj_to_inven(self_obj, obj);
   destroy_object(obj); //added by killap
   dude_caps_adjust(POWER_ARMOR_PRICE);
   call Node015;
end

procedure Node013 begin
   Reply(145);
   if (san_fran_flag(SF_GAS_BEGUN) and not san_fran_flag(SF_GAS_ELRONS)) then
      NOption(146, Node1014, 4);
   if (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL)) then
      NOption(147, Node0914, 4);
   NOption(mstr(148) + POWER_ARMOR_PRICE + mstr(173), Node015a, 4);
   NOption(149, Node016, 4);
end

procedure Node014 begin
   Reply(150);
   NOption(151, Node999, 4);
end

procedure Node015 begin
   Reply(152);
   NOption(153, Node999, 4);
end

procedure Node999a begin
   DownReactTiny;
   call Node999;
end

procedure Node016 begin
   Reply(154);
   NOption(155, Node999, 4);
   BOption(156, Node999a, 4);
end

procedure Node017 begin
   float_msg(self_obj, mstr(157), FLOAT_MSG_RED);
   call Node998;
end

procedure Node018 begin
   Reply(158);
   NOption(159, Node019, 4);
   if (dude_item(PID_HARDENED_POWER_ARMOR)) then
      NOption(128, Node013, 4);
end

procedure Node019 begin
   Reply(160);
   NOption(161, Node020, 4);
   NOption(162, NOde999, 4);
end

procedure Node020 begin
   if (bad_critter_reaction) then
      Reply(163);
   else
      Reply(164);
   NOption(165, Node007, 4);
   if (armor_flag_any == 0 and (tanker_flag(TANK_KNOWN) and not tanker_flag(TANK_FUEL)) ) then
      NOption(166, Node96a, 4);
   if (armor_flag_any == 0 and (GASBEGUN and not san_fran_flag(SF_GAS_ELRONS))) then
      NOption(167, Node108a, 4);
   NOption(168, Node999, 4);
end

procedure Node021 begin
   float_msg(self_obj, mstr(169), FLOAT_MSG_NORMAL);
//   Reply(169);
//   NOption(170, Node999, 1);
end

procedure Node022 begin
   float_msg(self_obj, mstr(174), FLOAT_MSG_NORMAL);
end

//added by killap
procedure doCallGuards begin
   set_san_fran_flag(SF_SHI_ENEMY);
end
//end added

//xxxxxxxxxxxxxxxxxxxx
