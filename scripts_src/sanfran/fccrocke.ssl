/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name: Crocket
        Location: San Fran ELron map
        Description: He will do hardened power armor upgrade

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "fccrocke"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_FCCROCKE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_SAN_FRANCISCO)

#include "../headers/command.h"
#include "../headers/modreact.h"
#include "../headers/sanfran.h"


// pbs:
#define POWER_ARMOR_HARDEN_COST     (6000) // was 10000


/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;
procedure combat_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;
procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;
procedure Node022;
procedure Node009b;
procedure doCallGuards;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Upgrade_Flag               (7)
#define LVAR_Timer                      (8)
#define LVAR_Node1_Count                (9)
#define LVAR_Node7_Count                (10)
#define LVAR_Armor_Known                (11)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable begin
   Only_Once:=0;
   callGuards := 0;
end

procedure combat_p_proc begin
   if (not critter_is_fleeing(self_obj)) then begin
      critter_set_flee_state(self_obj, 1);
   end
end

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_SAN_FRAN_ELRONS);
   set_self_ai(AI_PEASANT);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ((local_var(LVAR_Hostile) == 2) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Personal_Enemy,1);
   end

end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_TOLERANT;
   Karma_Perception:=KARMA_PERCEPTION1;

   CheckKarma;

   GetReaction;

   if (armor_flag(ARMOR_START)) then begin
      set_local_var(LVAR_Armor_Known, 1);
   end


   if (san_fran_flag(SF_ELRON_ENEMY)) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node017;
      gSay_End;
      end_dialogue;
   end else if (local_var(LVAR_Herebefore) == 1 and dude_iq < 4) then begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node002;
      gSay_End;
      end_dialogue;
   end else if (local_var(LVAR_Timer)) then begin
      if ((game_time - local_var(LVAR_Timer)) < ONE_GAME_DAY) then begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node018;
         gSay_End;
         end_dialogue;
      end else begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
            call Node019;
         gSay_End;
         end_dialogue;
      end
   end else if (/*armor_flag(ARMOR_SOLDFUELTANKER) or */armor_flag(ARMOR_SOLDMONEY)) then begin //killap's fix: commented out since this is the tanker variable and in addition, the elron variable is not needed since the fuel was requested for the spaceship and not Crocke (why would he get mad then?)
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node021;
      gSay_End;
      end_dialogue;
   end else begin
      start_gdialog(NAME,self_obj,4,-1,-1);
      gSay_Start;
         call Node001;
      gSay_End;
      end_dialogue;
   end

   //added by killap
   if (callGuards) then begin
      callGuards := 0;
      call doCallGuards;
   end
   //end added

   set_local_var(LVAR_Herebefore, 1);
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   if (local_var(LVAR_Herebefore) == 0) then
      display_msg(mstr(100));
   else
      display_msg(mstr(101));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(102));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end

procedure Node998a begin
   set_san_fran_flag(SF_ELRON_ENEMY);
   callGuards := 1;
   call Node998;
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node320 begin
   if (local_var(LVAR_Upgrade_Flag) < 2) then
      call Node003;
   else
      call Node020;
end

procedure Node001 begin
   if (local_var(LVAR_Node1_Count) == 0) then begin
      set_local_var(LVAR_Node1_Count, 1);
      Reply(103);
   end else begin
      Reply(182);
   end

   NLowOption(104, Node002);
   if (local_var(LVAR_Timer) == 0) then begin //added check by killap - only ask if he is not already making one.
      if (armor_flag(ARMOR_START)) then begin
         //NOption(105, Node003, 4);
         NOption(105, Node320, 4); //edit by killap
      end

      if (armor_flag_any or local_var(LVAR_Armor_Known)) then begin
         if (not local_var(LVAR_Upgrade_Flag)) then
            NOption(106, Node320, 4);
         else
            NOption(185, Node320, 4);
      end
   end
   NOption(107, Node004, 4);
   NOption(108, Node005, 4);
   NOption(109, Node999, 4);
end

procedure Node002 begin
   Reply(110);
   NOption(111, Node999, 4);
end

procedure Node003 begin
   Reply(112);
   if (armor_flag(ARMOR_START)) then
      NOption(113, Node006, 4);
   NOption(114, Node007, 4);
   BOption(115, NOde008, 4);
   NOption(116, Node999, 4);
end

procedure Node004 begin
   Reply(117);
   NOption(118, Node001, 4);
   NOption(119, Node999, 4);
end

procedure Node005 begin
   Reply(120);
   NOption(121, Node015, 4);
   NOption(122, Node001, 4);
   NOption(123, Node015, 4);
   NOption(124, Node016, 4);
   NOption(125, Node999, 4);
end

procedure Node006 begin
   Reply(126);
   if (dude_item(PID_POWERED_ARMOR)) then
      NOption(127, Node009b, 4);
   NOption(128, Node010, 4);
end

procedure Node009a begin
   variable obj;
   if (dude_item(PID_POWERED_ARMOR)) then begin
      set_local_var(LVAR_Timer, game_time);
      obj := dude_item(PID_POWERED_ARMOR);
      rm_obj_from_inven(dude_obj, obj);
      if (armor_flag(ARMOR_START)) then
         set_armor_flag(ARMOR_GOTTENPAID);
      destroy_object(obj);
      dude_caps_adjust(-POWER_ARMOR_HARDEN_COST);
      self_caps_adjust(POWER_ARMOR_HARDEN_COST);
      call Node009;
   end else begin
      call Node010;
   end
end

procedure Node009b begin
   variable obj;
   if (dude_item(PID_POWERED_ARMOR)) then begin
      set_local_var(LVAR_Timer, game_time);
      obj := dude_item(PID_POWERED_ARMOR);
      rm_obj_from_inven(dude_obj, obj);
      if (armor_flag(ARMOR_START)) then
         set_armor_flag(ARMOR_GOTTENPAID);
      destroy_object(obj);
      call Node009;
   end else begin
      call Node010;
   end
end

procedure Node007 begin
   if (local_var(LVAR_Node7_Count) == 0) then begin
      set_local_var(LVAR_Node7_Count, 1);
      Reply(sprintf(mstr(129), POWER_ARMOR_HARDEN_COST));
   end else begin
      set_local_var(LVAR_Node7_Count, 2);
      Reply(sprintf(mstr(183), POWER_ARMOR_HARDEN_COST));
   end
   if (local_var(LVAR_Node7_Count) == 1) then
      NOption(sprintf(mstr(130), POWER_ARMOR_HARDEN_COST), Node011, 4);
   if (dude_caps >= POWER_ARMOR_HARDEN_COST) then
      NOption(131, Node009a, 4);
   NOption(132, Node999, 4);
end

procedure Node1213 begin
   if (skill_success(dude_obj, SKILL_SPEECH, 0) and (local_var(LVAR_Upgrade_Flag) < 2)) then
      call Node012;
   else
      call Node013;
end

procedure Node008 begin
   Reply(133);
   BOption(134, Node1213, 4);
   NOption(135, Node001, 4);
end

procedure Node009 begin
   if (local_var(LVAR_Upgrade_Flag) == 0) then
      Reply(136);
   else
      Reply(184);

   inc_local_var(LVAR_Upgrade_Flag); //moved by killap from Node019
   NOption(137, Node001, 4);
   NOption(138, Node999, 4);
end

procedure Node010 begin
   Reply(139);
   NOption(140, Node001, 4);
   NOption(141, Node999, 4);
end

procedure Node011 begin
   Reply(sprintf(mstr(142), POWER_ARMOR_HARDEN_COST));
   NOption(143, Node999, 4);
   if (dude_caps >= POWER_ARMOR_HARDEN_COST) then
      NOption(144, Node009a, 4);
   NOption(145, Node999, 4);
end

procedure Node012 begin
   Reply(146);
   NOption(147, Node009b, 4);
   NOption(148, Node010, 4);
end

procedure Node013 begin
   Reply(149);
   BOption(150, Node998a, 4); //edit by killap - was NOption
   NOption(151, Node014, 4);
end

procedure Node014 begin
   Reply(152);
   NOption(153, Node998a, 4);
end

procedure Node015 begin
   Reply(154);
   NOption(155, Node001, 4);
   NOption(156, Node999, 4);
end

procedure Node016 begin
   set_local_var(LVAR_Armor_Known, 1);
   Reply(157);
   NOption(158, Node001, 4);
   NOption(159, Node999, 4);
end

procedure Node1819 begin
   if (game_time - local_var(LVAR_Timer) >= ONE_GAME_DAY) then
      call Node019;
   else
      call Node018;
end

procedure Node017 begin
   Reply(160);

   //NOption(161, Node001, 4);
   if (local_var(LVAR_Timer) and (local_var(LVAR_Upgrade_Flag) < 2)) then
      NOption(162, Node1819, 4);
   BOption(163, Node998a, 4); //edit by killap - was NOption
   NOption(164, Node999, 4);
end

procedure Node018 begin
   Reply(165);
//   NOption(166, Node001, 4);
   NOption(167, Node999, 4);
end

procedure Node019 begin
//   inc_local_var(LVAR_Upgrade_Flag); //fix by killap - moved to Node009
   //added by killap
   if (armor_flag(ARMOR_START)) then
      set_armor_flag(ARMOR_GOTTEN);
   //end added
   add_obj_to_inven(dude_obj, create_object(PID_HARDENED_POWER_ARMOR, 0, 0));
   set_local_var(LVAR_Timer, 0);
   Reply(168);
   NOption(169, Node001, 4);
   NOption(170, Node999, 4);
   BOption(171, Node998a, 4); //edit by killap - was NOption
end

procedure Node020 begin
   Reply(172);
   NOption(173, Node001, 4);
   BOption(174, Node998a, 4); //edit by killap - was NOption
   NOption(175, Node999, 4);
end

procedure Node021 begin
   Reply(176);
   //NOption(177, Node999, 4);
   NOption(177, Node001, 4);
   NOption(178, Node022, 4);
end

procedure Node999a begin
   set_san_fran_flag(SF_ELRON_ENEMY);
   callGuards := 1;
   call Node999;
end

procedure Node022 begin
   Reply(179);
   BOption(180, Node998a, 4); //edit by killap - was NOption
   NOption(181, NOde999a, 4);
end

procedure doCallGuards begin
   SET_GUARD_NOTIFY(dude_obj);
end

//xxxxxxxxxxxxxxxxxxxx
