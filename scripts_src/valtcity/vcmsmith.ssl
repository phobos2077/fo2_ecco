/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "vcmsmith"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_VCMSMITH
#define TOWN_REP_VAR            (GVAR_TOWN_REP_VAULT_CITY)

#include "../headers/command.h"
#include "../headers/modreact.h"

//#define PID_SMITH_COOL_ITEM             (3)

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node008a;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node013;
procedure Node014;
procedure Node014a;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node017a;
procedure Node018;
procedure Node019;
procedure Node020;

//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Plow_Date                  (7)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_VAULT_CITIZENS);
   set_self_ai(AI_PEASANT);
end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ( ( local_var(LVAR_Hostile) == 2 ) or ( ( global_var( GVAR_ENEMY_VAULT_COURTYARD ) == 1 ) ) ) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end
end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_global_var( GVAR_ENEMY_VAULT_COURTYARD, 1 );
      set_global_var( GVAR_ENEMY_VAULT_CITY, 1 );
      set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_KICKED_OUT );
      set_global_var( GVAR_TOWN_REP_VAULT_CITY, (global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_KICKED_OUT ) );
      set_global_var( GVAR_VAULT_CITIZENSHIP, 0 );
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION0;

   CheckKarma;

   GetReaction;

   if (global_var(GVAR_VAULT_PLOW_PROBLEM) == PLOW_FIXED) then begin
       floater(165);
   end
   else if (global_var(GVAR_VAULT_PLOW_PROBLEM) == PLOW_NEED_REWARD) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node020;
       gSay_End;
       end_dialogue;
   end
   else if ((global_var(GVAR_VAULT_PLOW_PROBLEM) == PLOW_HELP_CASH) or (global_var(GVAR_VAULT_PLOW_PROBLEM) == PLOW_HELP_FREE)) then begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node017;
       gSay_End;
       end_dialogue;
   end
   else if ((global_var(GVAR_VAULT_PLOW_PROBLEM) == PLOW_SCREW_SMITH) or town_rep_is_hated) then begin
       floater(random(143,144));
   end
   else begin
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end
end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   if (local_var(LVAR_Herebefore) == 0) then begin
       set_local_var(LVAR_Herebefore,1);
       Reply(102);
   end
   else
       Reply(103);

   call Node002;
end

procedure Node002 begin
   NOption(104,Node003,004);
   NOption(105,Node004,004);
   if (vault_13_visible_on_map == 0) then
       NOption(106,Node006,004);
   if (global_var(GVAR_VAULT_PLOW_PROBLEM) == PLOW_PLAYER_KNOWS) then
       NOption(107,Node013,004);
   if (global_var(GVAR_VAULT_PLOW_PROBLEM) == PLOW_WIFE_TALKED) then
       NOption(108,Node008,004);
   NOption(109,Node999,004);
   NLowOption(110,Node005);
end

procedure Node003 begin
   Reply(111);

   NOption(112,Node007,004);
   NOption(113,Node004,004);
   NOption(114,Node009,004);
   NOption(115,Node999,004);
end

procedure Node004 begin
   Reply(116);

   NOption(117,Node008,004);
   NOption(118,Node007,004);
   NOption(119,Node009,004);
   NOption(120,Node999,004);
end

procedure Node005 begin
   NMessage(121);
end

procedure Node006 begin
   Reply(122);

   NOption(123,Node009,004);
   NOption(124,Node999,004);
end

procedure Node007 begin
   Reply(125);

   NOption(126,Node010,004);
   NOption(127,Node011,004);
   NOption(128,Node009,004);
   NOption(129,Node999,004);
end

procedure Node008 begin
   set_global_var(GVAR_VAULT_PLOW_PROBLEM,PLOW_PLAYER_KNOWS);

   Reply(130);

   NOption(131,Node013,004);
   NOption(132,Node009,004);
   BOption(133,Node008a,004);
end

procedure Node008a begin
   set_global_var(GVAR_TOWN_REP_VAULT_CITY,(global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_SCREW_SMITHS));
end

procedure Node009 begin
   Reply(134);

   call Node002;
end

procedure Node010 begin
   Reply(135);

   NOption(136,Node011,004);
   NOption(137,Node009,004);
   NOption(138,Node999,004);
end

procedure Node011 begin
   set_global_var(GVAR_VAULT_PLOW_PROBLEM,PLOW_PLAYER_KNOWS);

   Reply(139);

   NOption(140,Node013,004);
   NOption(141,Node009,004);
   BOption(142,Node008a,004);
end

procedure Node013 begin
   Reply(145);

   BOption(146,Node014,004);
   NOption(147,Node015,004);
   NOption(148,Node009,004);
   BOption(149,Node008a,004);
end

procedure Node014 begin
   Reply(150);

   BOption(151,Node014a,004);
   NOption(152,Node015,004);
   BOption(153,Node008a,004);
end

procedure Node014a begin
   dude_caps_adjust( 150);
   set_local_var(LVAR_Plow_Date,game_time);
   set_global_var(GVAR_VAULT_PLOW_PROBLEM,PLOW_HELP_CASH);
   call Node008a;
   call Node016;
end

procedure Node015 begin
   set_global_var(GVAR_VAULT_PLOW_PROBLEM,PLOW_HELP_FREE);
   set_global_var(GVAR_TOWN_REP_VAULT_CITY,(global_var(GVAR_TOWN_REP_VAULT_CITY) + REP_TOWN_SMITH_HELP_FREE ));
   inc_general_rep( REP_BONUS_PLOW_FOR_FREE );
   Reply(154);

   NOption(155,Node016,004);
end

procedure Node016 begin
   Reply(156);

   NOption(157,Node999,004);
end

procedure Node017 begin
   Reply(158);

   NOption(159,Node018,004);
   NOption(160,Node017a,004);
end

procedure Node017a begin
   if (global_var(GVAR_VAULT_PLOW_PROBLEM) == PLOW_HELP_FREE) then begin
       set_global_var(GVAR_VAULT_PLOW_PROBLEM,PLOW_PLAYER_KNOWS);
       call Node018;
   end
   else
       call Node019;
end

procedure Node018 begin
   NMessage(161);
end

procedure Node019 begin
   set_global_var(GVAR_VAULT_PLOW_PROBLEM,PLOW_SCREW_SMITH);
   set_global_var(GVAR_TOWN_REP_VAULT_CITY,(global_var(GVAR_TOWN_REP_VAULT_CITY) + 2*REP_TOWN_SCREW_SMITHS));

   BMessage(162);
end

procedure Node020 begin
   variable item;

   item:=create_object(PID_DESERT_EAGLE_EXT_MAG,0,0); // pbs: changed from regular Desert Eagle
   add_mult_objs_to_inven(dude_obj,item,1);
   item:=create_object(PID_44_MAGNUM_JHP,0,0);
   add_mult_objs_to_inven(dude_obj,item,2); // pbs: increased from 1
   set_global_var(GVAR_VAULT_PLOW_PROBLEM,PLOW_FIXED);

   set_global_var(GVAR_TOWN_REP_VAULT_CITY,(global_var(GVAR_TOWN_REP_VAULT_CITY) - 3*REP_TOWN_SCREW_SMITHS));

   display_msg( mstr(166) );
   give_xp( EXP_PLOW_FOR_SMITHS );

   Reply(163);

   NOption(164,Node999,004);
end

//xxxxxxxxxxxxxxxxxxxx