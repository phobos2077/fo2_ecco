/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/******************************************************************************************
        Item: Box For Office Darrow
        Location : Vault City
        Description: This box is for Sub Amenities Officer Darrow's Goods

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: August 21, 1998

           Updated:
******************************************************************************************/


/* Include Files */
#define SCRIPT_REALNAME "vidarbox"
#include "../headers/define.h"

#define NAME                    SCRIPT_VIDARBOX

#include "../headers/command.h"
#include "../headers/vctydwtn.h"
/* Defines and Macros */

#define LVAR_Restock_Time                 (0)
#define LVAR_Restock_Time_Fix             (1) //added by killap

/* Standard Script Procedures */
procedure start;
procedure map_enter_p_proc;
procedure map_update_p_proc;
procedure pickup_p_proc;

import variable vault_city_darrow_box;

procedure start begin
   vault_city_darrow_box := self_obj;
end

procedure map_enter_p_proc begin
   vault_city_darrow_box := self_obj;
   if (is_loading_game == false) then begin
      restock_fix
      if (local_var(LVAR_Restock_Time) < game_time) then begin
         variable tmp_box := move_critical_items();
         check_restock_item(PID_BOTTLE_CAPS, 100, 500, 100)
         check_restock_item(PID_CROWBAR, 1, 1, 100)
         check_restock_item(PID_SLEDGEHAMMER, 1, 1, 100)
         check_restock_item(PID_MULTI_TOOL, 1, 1, 100)
         check_restock_item(PID_FLARE, 2, 5, 100)
         check_restock_item(PID_KNIFE, 1, 1, 100)
         check_restock_item(PID_ROPE, 1, 3, 50) // pbs: max from 1
         check_restock_item(PID_PUMP_PARTS, 1, 3, 50) // pbs
         check_restock_item(PID_LIGHTER, 1, 1, 25)
         call restore_critical_items(tmp_box);
         set_local_var(LVAR_Restock_Time, (random(2,3) * ONE_GAME_DAY) + game_time);
      end
   end
end

procedure map_update_p_proc begin
   vault_city_darrow_box := self_obj;
end

procedure pickup_p_proc begin
   if(source_obj == dude_obj) then begin
      if( is_success( roll_vs_skill( dude_obj, SKILL_STEAL, -20 ) ) == FALSE ) then begin
         script_overrides;
         set_map_var(MVAR_Stealing_From_Darrow, 1 );
      end
   end
end
