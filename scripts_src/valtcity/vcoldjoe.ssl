/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/*
        Name:
        Location:
        Description:

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created:

           Updated:
*/

/* Include Files */

/* Note, the Following Lines need to be in this order so that
   the script will be compilable. The define Name is referenced
   in a module from define.h and used in command.h. Please do
   not change the ordering.
        -rwh2   11/13/97
*/
#define SCRIPT_REALNAME "vcoldjoe"
#include "../headers/define.h"
//#include "../headers/<TownName.h>"

#define NAME                    SCRIPT_VCOLDJOE
#define TOWN_REP_VAR            (GVAR_TOWN_REP_VAULT_CITY)

#include "../headers/command.h"
#include "../headers/modreact.h"
#include "../headers/vill.h"

#define PID_IAN_GUN             (PID_10MM_PISTOL)

/* Standard Script Procedures */
procedure start;
procedure critter_p_proc;
procedure pickup_p_proc;
procedure talk_p_proc;
procedure destroy_p_proc;
procedure look_at_p_proc;
procedure description_p_proc;
procedure use_skill_on_p_proc;
procedure damage_p_proc;
procedure map_enter_p_proc;

/* Script Specific Procedure Calls */
procedure Node998;                                      // This Node is Always Combat
procedure Node999;                                      // This Node is Always Ending


// The next lines are added in by the Designer Tool.
// Do NOT add in any lines here.
//~~~~~~~~~~~~~~~~ DESIGNER TOOL STARTS HERE

procedure Node001;
procedure Node002;
procedure Node003;
procedure Node004;
procedure Node005;
procedure Node006;
procedure Node007;
procedure Node008;
procedure Node009;
procedure Node010;
procedure Node011;
procedure Node012;
procedure Node013;

procedure Node014;
procedure Node015;
procedure Node016;
procedure Node017;
procedure Node018;
procedure Node019;
procedure Node020;
procedure Node021;

procedure Node022;
procedure Node023;
procedure Node024;

// by Lujo
procedure Node025; 
procedure Node026; 
//~~~~~~~~~~~~~~~~ DESIGN TOOL ENDS HERE
// The Following lines are for anything that is not needed to be
// seen by the design Tool


/* Local Variables which are saved. All Local Variables need to be
   prepended by LVAR_ */
#define LVAR_Herebefore                 (4)
#define LVAR_Hostile                    (5)
#define LVAR_Personal_Enemy             (6)
#define LVAR_Given_Gun                  (7)
#define LVAR_Know_Real_Name             (8)

/* Imported variables from the Map scripts. These should only be
   pointers and variables that need not be saved. If a variable
   Needs to be saved, make it a map variable (MVAR_) */


/* Local variables which do not need to be saved between map changes. */
variable Only_Once:=0;

procedure start begin
end

/* This procedure will get called each time that the map is first entered. It will
   set up the Team number and AI packet for this critter. This will override the
   default from the prototype, and needs to be set in scripts. */
procedure map_enter_p_proc begin
   Only_Once:=0;
   set_self_team(TEAM_VAULT_OUTSIDER);
   set_self_ai(AI_COWARD);

   //added by killap - expansion pack
   if (global_var(GVAR_QUEST_VAULT_VILLAGE) < VILLAGE_QUEST_GET_GUNS) then
      set_self_invisible; //invisible
   else begin
      set_self_visible; //visible
      if( MacRae_Ptr != 0 ) then begin
         move_to(self_obj,16255,0);
      end
   end
   //end

end


/* Every heartbeat that the critter gets, this procedure will be called. Anything from
   Movement to attacking the player on sight can be placed in here.*/
procedure critter_p_proc begin

/* If the critter is mad at the player for any reason, it will attack and remember to attack
   the player should the game be saved and loaded repeatedly. Additionally, if any special
   actions need to be taken by the critter based on previous combat, the critter will remember
   this as well. */

   if ( ( (local_var(LVAR_Hostile) == 2) or ( global_var( GVAR_ENEMY_VAULT_CITY ) == 1 ) ) and (self_can_see_dude)) then begin
       set_local_var(LVAR_Hostile,1);
       attack(dude_obj);
   end

end

/* Whenever the critter takes damage of any type, this procedure will be called. Things
   like setting ENEMY_ and LVAR_Personal_Enemy can be set here. */
procedure damage_p_proc begin

/* If the player causes damage to this critter, then he will instantly consider the player
   his personal enemy. In Critter_Proc or through dialog, actions will be taken against
   the player for his evil acts. */
   if (source_obj == dude_obj) then begin
      set_local_var(LVAR_Personal_Enemy,1);
      set_global_var( GVAR_ENEMY_VAULT_CITY, 1 );
      set_global_var( GVAR_VAULT_CITIZEN, CITIZEN_KICKED_OUT );
      set_global_var( GVAR_VAULT_CITIZENSHIP, 0 );
   end
end

/* Any time that the player is caught stealing from this critter, Pickup_proc will be called.
   In here, various things can happen. The most common response is instant hostility which
   will be remembered. */
procedure pickup_p_proc begin
   if (source_obj == dude_obj) then begin
       set_local_var(LVAR_Hostile,2);
   end
end

/* The dialog system is setup and prepares the player to talk to this NPC. Where To Go
   written by designers are placed in here. Additionally, Reactions are generated and
   stored which affects player interactions. */
procedure talk_p_proc begin
   Evil_Critter:=0;
   Slavery_Tolerant:=SLAVE_INTOLERANT;
   Karma_Perception:=KARMA_PERCEPTION5;

   CheckKarma;

   GetReaction;

   if (dude_iq < 4) then begin
       call Node012;
   end

   if (local_var(LVAR_Herebefore) == 0) then begin
       set_local_var(LVAR_Herebefore,1);
       start_gdialog(NAME,self_obj,4,-1,-1);
       gSay_Start;
           call Node001;
       gSay_End;
       end_dialogue;
   end else begin
      // by Lujo begin
      if not (mole_rat_origin_info) then begin
         start_gdialog(NAME,self_obj,4,-1,-1);
         gSay_Start;
             call Node025;
         gSay_End;
         end_dialogue;
      end else
     // by Lujo end;
      if (local_var(LVAR_Given_Gun) == 1) then begin
         call Node013;
      end else begin
         call Node012;
      end
   end

end

/* This procedure gets called only on the death of this NPC. Special things like
   incrementing the death count for reputation purposes and Enemy Counters are placed
   in here. */
procedure destroy_p_proc begin

/* Increment the aligned critter counter*/
   inc_good_critter

/* Set global_variable for Enemy status*/
end

/* Look_at_p_proc gets called any time that the player passes the cursor over any object.
   This should only hold the most cursory of glances for the player. */
procedure look_at_p_proc begin
   script_overrides;
   display_msg(mstr(100));
end

/* The player will see more indepth descriptions from this procedure. They are actively
   looking at the critter and want more information. Things like names can be added here
   if the critter is known to the player. */
procedure description_p_proc begin
   script_overrides;
   display_msg(mstr(101));
end

/* Any time a skill is used on a critter this call is made. This can be to give examinations
   for things like Doctor skill or messages for various other skills. */
procedure use_skill_on_p_proc begin
end

/* Should the Player ever cause the NPC too much discomfort that he desires to attack the player,
   this call will be made. Essentially, it stores the Hostile vaule so that the critter remembers
   he was once hostile towards the player.*/
procedure Node998 begin
   set_local_var(LVAR_Hostile,2);
end

/* Anytime that there is a need for an ending to dialog, this node is to be called. It will just
   exit from the dialog system without any reprisals from the NPC. */
procedure Node999 begin
end


// Not lines are allowed to be added below here
// The Following lines are from the Design Tool
//~~~~~~~~~~~~~~~~ DESIGN TOOL STARTS HERE

procedure Node001 begin
   Reply(102);

   NOption(103,Node002,004);
   NOption(104,Node003,006);
end

procedure Node002 begin
   Reply(105);

   NOption(106,Node004,006);
   NOption(107,Node005,004);
end

procedure Node003 begin
   Reply(108);

   NOption(109,Node006,008);
   NOption(110,Node004,006);
end

procedure Node004 begin
   Reply(111);

   NOption(112,Node007,004);
end

procedure Node005 begin
   Reply(113);

   //well wait - expansion - killap
   //if (global_var(GVAR_QUEST_VAULT_VILLAGE) < VILLAGE_QUEST_DELIVERED_GUNS) then
      NOption(147,Node014,004);
   //else
     NOption(114,Node999,004);
end

procedure Node006 begin

   gfade_out(600);
   gfade_in(600);

   Reply(115);

   NOption(116,Node008,004);
end

procedure Node007 begin
   Reply(117);

   //hmm ok, but wait - expansion - killap
   //if (global_var(GVAR_QUEST_VAULT_VILLAGE) < VILLAGE_QUEST_DELIVERED_GUNS) then
      NOption(146,Node014,004);
   //else
      NOption(118,Node999,006);
end

procedure Node008 begin
   Reply(119);
   set_local_var(LVAR_Know_Real_Name,1);
   NOption(120,Node009,004);
end

procedure Node009 begin
   Reply(121);

   NOption(122,Node010,004);
end

procedure Node010 begin
   Reply(123);

   NOption(124,Node011,004);
end

procedure Node011 begin
   variable item;

   set_local_var(LVAR_Given_Gun,1);

   item:=create_object(PID_IAN_GUN,0,0);
   add_mult_objs_to_inven(dude_obj,item,1);

   Reply(125);
   //expansion - killap
   //if (global_var(GVAR_QUEST_VAULT_VILLAGE) < VILLAGE_QUEST_DELIVERED_GUNS) then
      NOption(133,Node014,004);
   //else
      NOption(126,Node999,008);
end

procedure Node012 begin
   floater(random(127,129));
end

procedure Node013 begin
   floater(random(130,132));
end

//xxxxxxxxxxxxxxxxxxxx

//added by killap - expansion pack
procedure Node014 begin
   Reply(134);
   if (global_var(GVAR_QUEST_VAULT_VILLAGE) >= VILLAGE_QUEST_SPEAK_WITH_COUNCIL) then
      NOption(150,Node019,004);
   else
      NOption(135,Node015,004);

end
procedure Node015 begin
   set_global_var(GVAR_QUEST_VAULT_VILLAGE, VILLAGE_QUEST_JOE_SPOKE);
   Reply(136);

   NOption(137,Node999,004);
   NOption(138,Node016,004);
end

procedure Node016 begin
   Reply(139);

   NOption(140,Node999,004);
   NOption(141,Node017,004);
   if (local_var(LVAR_Know_Real_Name)) then
      NOption(200,Node022,004);
end

procedure Node017 begin
   if (local_var(LVAR_Know_Real_Name) == 1) then
      NOption(200,Node022,004);
   Reply(142);

   NOption(143,Node018,004);
end

procedure Node018 begin
   //set_global_var(GVAR_VAULT_VILLAGE_MOLERAT_ORIGIN_KNOWN,GAVE_INFO);
   set_mole_rat_origin_info;

   Reply(144);

   if (local_var(LVAR_Know_Real_Name)) then
      NOption(145,Node999,004);
   else
      NOption(148,Node999,004);
    //CHECK THIS AREA - killap -8/4/07
    //need to make a var here so that this can be used with mcclure. At the moment
    //you can always say to mcclure that you know what is going on.
    //also shouldn't be called Ian if you don't acutally know that...
   //set_global_var(GVAR_
   //Lujo: I used the molerat_origin_info to allow me to get molerat_origin_amulet from Connor and used both to get McClure to open up the diplomatic options.
end
//end

procedure Node019 begin
   Reply(151);
   //NOption(155,Node999,004);
   //NOption(156,Node017,004);
   //if (local_var(LVAR_Know_Real_Name)) then
   //   NOption(200,Node022,004);
   if (global_var(GVAR_MCCLURE_KNOWN) >= MCCLURE_KNOWN) then
      NOption(153,Node020,004);
   else
      NOption(152,Node021,004);
end

procedure Node020 begin
   Reply(157);
   if (local_var(LVAR_Know_Real_Name)) then
      NOption(200,Node022,004);
   NOption(158,Node999,004);
   NOption(159,Node017,004);
end

procedure Node021 begin
   Reply(154);
   if (local_var(LVAR_Know_Real_Name)) then
      NOption(200,Node022,004);
   NOption(155,Node999,004);
   NOption(156,Node017,004);
end

procedure Node022 begin
   Reply(201);
   NOption(202,Node023,004);
   set_local_var(LVAR_Know_Real_Name,2);
end

procedure Node023 begin
   Reply(203);
   NOption(204,Node017,004);
   NOption(205,Node024,004);
end

procedure Node024 begin
   Reply(206);
   NOption(207,Node999,004);
   NOption(208,Node017,004);
end

//Lujo: I added 2 nodes in case you miss the molerat_origin_info when you first talk to joe (and I used those for McClure diplomacy). You can access these only once, and then it's floaters again.

procedure Node025 begin
   Reply(301);
   NOption(302,Node026,004);
end

procedure Node026 begin
   set_mole_rat_origin_info;
   Reply(303);
   NOption(304,Node999,004);
end