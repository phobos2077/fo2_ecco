/**
 * Ammo cost mod.
 * Increases single shot ammo cost for weapons configured in INI.
 */

#include "../sfall/sfall.h"
#include "../sfall/lib.misc.h"


#define INI_FILE                "combat.ini"
#define INI_AMMOCOST_SECTION    "AMMOCOST"

variable
   ini_ammocost;

procedure start;

/*
   HOOK_AMMOCOST
   Runs when calculating ammo cost for a weapon. Doesn't affect damage, only how much ammo is spent. By default, weapons can make attacks when at least 1 ammo is left, regardless of ammo cost calculations. To add proper check for ammo before attacking and proper calculation of the number of burst rounds (hook type 1 and 2 in arg3), set CheckWeaponAmmoCost=1 in Misc section of ddraw.ini.

   Item    arg0 - The weapon
   int     arg1 - Number of bullets in burst or 1 for single shots
   int     arg2 - The amount of ammo that will be consumed, calculated by the original ammo cost function (this is basically 2 for Super Cattle Prod and Mega Power Fist)
                  NOTE: for hook type 2, this value is the ammo cost per round (default is always 1)
   int     arg3 - Type of hook:
                  0 - when subtracting ammo after single shot attack
                  1 - when checking for "out of ammo" before attack
*/
procedure ammocost_handler begin
   variable weapon, rounds, vanillaCost, type, newCost;

   weapon := get_sfall_arg;
   rounds := get_sfall_arg;
   vanillaCost := get_sfall_arg;
   type := get_sfall_arg;
   
   if (weapon) then begin
      variable pid, mult;
      pid := obj_pid(weapon);
      newCost := ini_ammocost[pid];
      if (newCost) then begin
         // debug_msg("override cost: "+newCost);
         set_sfall_return(rounds * newCost);
      end
   end
end

procedure start begin
   if game_loaded then begin
      ini_ammocost := get_ini_section_int_to_int(INI_FILE, INI_AMMOCOST_SECTION, true);
      if (len_array(ini_ammocost) > 0) then begin
         register_hook_proc(HOOK_AMMOCOST, ammocost_handler);
      end
   end
end
