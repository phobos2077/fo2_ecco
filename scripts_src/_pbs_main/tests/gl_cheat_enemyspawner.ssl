/*
   
*/

#include "..\..\sfall\sfall.h"
#include "..\..\sfall\dik.h"
#include "..\..\sfall\define_lite.h"
#include "..\..\sfall\lib.inven.h"
#include "..\..\headers\critrpid.h"
#include "..\..\headers\itempid.h"
#include "..\..\headers\scripts.h"

#define ENEMY_ENCLAVE   (1)
#define ENEMY_THUG      (2)
#define ENEMY_FARMER    (3)
#define ENEMY_EYEBOT    (4)
#define ENEMY_RAT       (5)
#define ENEMY_ALIEN     (6)

variable enemy_types;

procedure spawn_enemy(variable type, variable tile) begin
   variable critter, pid, sid, weaponPid, weapon;
   switch (type) begin
      case ENEMY_ENCLAVE:
         pid := PID_MALE_ENCALVE_PATROL + random(0, 1);
         sid := SCRIPT_ECELVPAT; // ECElvPat
         weaponPid := PID_PLASMA_PISTOL; // plasma pistol
      case ENEMY_THUG:
         pid := PID_GUN_THUG_MALE + random(0, 1);
         sid := SCRIPT_ECBANDIT; // ecbandit
         weaponPid := PID_DESERT_EAGLE; // DE
      case ENEMY_FARMER:
         pid := PID_MALE_FARMER + random(0, 1);
         sid := SCRIPT_DCPEASNT;
         weaponPid := PID_SAWED_OFF_SHOTGUN;
      case ENEMY_EYEBOT:
         pid := PID_EYEBALL;
         sid := SCRIPT_WCSECBOT;
      case ENEMY_RAT:
         pid := PID_RAT;
         sid := SCRIPT_ZCLRAT;
      case ENEMY_ALIEN:
         pid := PID_ALIEN;
         sid := SCRIPT_ECALIEN;
      default:
         return;
   end

   critter := create_object_sid(pid, tile, dude_elevation, sid);
   if (weaponPid) then begin
      weapon := add_item_pid(critter, weaponPid);
      wield_obj_critter(critter, weapon);
      call add_items_pid(critter, get_proto_data(weaponPid, PROTO_WP_AMMO_PID), 3);
   end
end

procedure spawn_enemies_circle(variable type, variable radius) begin
   variable dir, origin := tile_under_cursor;
   call spawn_enemy(type, origin);
   if (radius > 0) then begin
      for (dir := 0; dir < 6; dir++) begin
         call spawn_enemy(type, tile_num_in_direction(origin, dir, radius));
      end
   end
end

procedure keypress_handler begin
   variable
      isPressed := get_sfall_arg,
      keyCode := get_sfall_arg;

   if isPressed and get_cursor_mode == 0 then begin
      if (keyCode == DIK_5) then
         call spawn_enemies_circle(ENEMY_ENCLAVE, 2);
      if (keyCode == DIK_6) then
         call spawn_enemies_circle(ENEMY_THUG, 2);
      if (keyCode == DIK_7) then
         call spawn_enemy(ENEMY_FARMER, tile_under_cursor);
      if (keyCode == DIK_8) then
         call spawn_enemy(ENEMY_EYEBOT, tile_under_cursor);
      if (keyCode == DIK_9) then
         call spawn_enemies_circle(ENEMY_ALIEN, 1);
   end
end

procedure start begin
   if (not game_loaded) then return;

   register_hook_proc(HOOK_KEYPRESS, keypress_handler);
end
