/*  

   AP costs mod for fallout 2 by phobos2077
   ----------------------------------------
   
   - lower cost for unarmed special attacks
   - higher reload cost
   
   Requires sfall 3.5 or higher

*/

#include "../sfall/sfall.h"
#include "../sfall/lib.inven.h"
#include "../sfall/lib.math.h"
#include "../_pbs_headers/ecco.h"


variable
   ini_unarmed_special_attacks,
   ini_reload_cost;

procedure apcost_handler begin
   variable critter, type, aimed, weapon, normalCost, cost;
   critter := get_sfall_arg;
   type := get_sfall_arg;
   aimed := get_sfall_arg;
   normalCost := get_sfall_arg;

   cost := -1;
   if (ini_unarmed_special_attacks > 0) then begin
      if (type == ATKTYPE_PALMSTRIKE) or (type == ATKTYPE_PIERCINGSTRIKE) or (type == ATKTYPE_JAB) then begin
         cost := 4 - (has_trait(TRAIT_PERK, dude_obj, PERK_bonus_hth_attacks) > 0);
      end
      if (type == ATKTYPE_HIPKICK) or (type == ATKTYPE_HOOKKICK) or (type == ATKTYPE_PIERCINGKICK) then begin
         cost := 5 - (has_trait(TRAIT_PERK, dude_obj, PERK_weapon_fast_reload) > 0);
      end
      if (cost != -1 and aimed) then 
         cost++;
   end
   if (ini_reload_cost >= 0) then begin
      if (type == ATKTYPE_LWEP_RELOAD or type == ATKTYPE_RWEP_RELOAD) then begin
         cost := ini_reload_cost;
         if (normalCost == 1) then 
            cost := round(cost/2);
      end
   end
   if (cost != -1) then begin 
      set_sfall_return(cost);
   end
end


variable 
   useobj_time,
   useobj_ap,
   useobj_count;


#define load_ini_int(name)       int_from_ini_file(name, INI_COMBAT, "APCOST")

procedure start begin
   if game_loaded then begin
      load_ini_int(unarmed_special_attacks);
      load_ini_int(reload_cost);
      if (ini_unarmed_special_attacks > 0 or ini_reload_cost != -1) then begin
         register_hook_proc(HOOK_CALCAPCOST, apcost_handler);
      end
   end
end

