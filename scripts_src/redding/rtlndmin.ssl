/*
	Copyright 1998-2003 Interplay Entertainment Corp.  All rights reserved.
*/

/******************************************************************************************
        Item: Wood Door
        Locked: NO
        Trapped: NO
        Closing: YES
        Description: This is a generic locked and trapped wooden door.

        Log:
           Please note any changes that have been made to the file in Updated. Then comment
           the code which you have changed/altered/commented out. Please, do not delete any
           code which was written.

           Created: September 26, 1997

           Updated:
******************************************************************************************/


/* Include Files */
#include "..\headers\define.h"
#include "..\headers\redwame.h"

#define NAME                    SCRIPT_SPRTRP0
#define CUR_SCRIPT              SCRIPT_RTLNDMIN

#include "..\headers\command.h"

// Tile Nums for the Trap
#define LANDMINE_TILE1          (23287)  //was 22387 - killap
#define LANDMINE_TILE2          (23491)
#define LANDMINE_TILE3          (23091)
#define LANDMINE_TILE4          (22491)
#define LANDMINE_TILE5          (23498)
#define LANDMINE_TILE6          (24303)
#define LANDMINE_TILE7          (25107)
#define LANDMINE_TILE8          (26313)
#define LANDMINE_TILE9          (20910)
#define LANDMINE_TILE10         (17702) //was 17503 - killap
#define LANDMINE_TILE11         (16100)
#define LANDMINE_TILE12         (13898)
#define LANDMINE_TILE13         (18667)
#define LANDMINE_TILE14         (17259)
#define LANDMINE_TILE15         (26111)

// Trap Information
#define TRAPS_BONUS                     (-30)
#define PERCEPTION_BONUS                (-4)
#define AGILITY_BONUS                   (0)
#define DIST_BONUS                      (2)

#define TRAP_EXP                        EXP_TRAPS_NEG_30
#define TRAP_TIME                       (0)             // In Seconds

#define MIN_DMG                         (40)
#define MAX_DMG                         (60)
#define DAMAGE_TYPE                     DMG_explosion

#define MINE_ACTIVE                     (0)
#define MINE_DISARMED                   (1)
#define MINE_EXPLODED                   (2)

// Trap Art
#define ART_TRAP_VISIBLE                (PID_CAVE_FLOOR_TRAP_VISIBLE)
#define ART_TRAP_EXPLODED               (PID_CAVE_FLOOR_TRAP_DEPRESSED) // edit by killap - swapped with the one below
#define ART_TRAP_DISARMED               (PID_CAVE_FLOOR_TRAP_DISARMED)  // edit by killap - swapped with the one above


#define Which_Tile_Var(x)       if (x == LANDMINE_TILE1) then          \
                                   Landmine_Var:=MVAR_Landmine1;       \
                                else if (x == LANDMINE_TILE2) then     \
                                   Landmine_Var:=MVAR_Landmine2;       \
                                else if (x == LANDMINE_TILE3) then     \
                                   Landmine_Var:=MVAR_Landmine3;       \
                                else if (x == LANDMINE_TILE4) then     \
                                   Landmine_Var:=MVAR_Landmine4;       \
                                else if (x == LANDMINE_TILE5) then     \
                                   Landmine_Var:=MVAR_Landmine5;       \
                                else if (x == LANDMINE_TILE6) then     \
                                   Landmine_Var:=MVAR_Landmine6;       \
                                else if (x == LANDMINE_TILE7) then     \
                                   Landmine_Var:=MVAR_Landmine7;       \
                                else if (x == LANDMINE_TILE8) then     \
                                   Landmine_Var:=MVAR_Landmine8;       \
                                else if (x == LANDMINE_TILE9) then     \
                                   Landmine_Var:=MVAR_Landmine9;       \
                                else if (x == LANDMINE_TILE10) then     \
                                   Landmine_Var:=MVAR_Landmine10;       \
                                else if (x == LANDMINE_TILE11) then     \
                                   Landmine_Var:=MVAR_Landmine11;       \
                                else if (x == LANDMINE_TILE12) then     \
                                   Landmine_Var:=MVAR_Landmine12;       \
                                else if (x == LANDMINE_TILE13) then     \
                                   Landmine_Var:=MVAR_Landmine13;       \
                                else if (x == LANDMINE_TILE14) then     \
                                   Landmine_Var:=MVAR_Landmine14;       \
                                else if (x == LANDMINE_TILE15) then     \
                                   Landmine_Var:=MVAR_Landmine15

procedure start;
procedure spatial_p_proc;
procedure use_skill_on_p_proc;
procedure Check_Active(variable Mine_Var);
procedure Trap_Detection;


// Local Vars
#define LVAR_Trap_Found                 (0)
#define LVAR_Trap_Exploded              (1)
#define LVAR_Gave_Exp                   (2)
#define LVAR_Got_Perception_Roll        (3)
#define LVAR_Extended_Look              (4)


procedure start begin
end

procedure spatial_p_proc begin
   variable Landmine_Var;

   if (global_var(GVAR_FROG_MORTON) != 0) then begin
       call Trap_Detection;

       if (tile_distance_objs(self_obj,source_obj) == 0) then begin
           Which_Tile_Var(tile_num(self_obj));
           call Check_Active(Landmine_Var);
       end
   end
end

procedure use_skill_on_p_proc begin
   variable Traps_Roll;
   variable Trap;
   variable Landmine_Var;

   if (action_being_used == SKILL_TRAPS) then begin
       Which_Tile_Var(tile_num(self_obj));
       if (map_var(Landmine_Var) == MINE_ACTIVE) then begin
           script_overrides;
           Traps_Roll:=roll_vs_skill(source_obj,SKILL_TRAPS,TRAPS_BONUS);

           if (is_success(Traps_Roll)) then begin
               //Trap:=create_object_sid(ART_TRAP_DISARMED,tile_num(self_obj),elevation(self_obj),CUR_SCRIPT);
               Trap:=create_object(ART_TRAP_DISARMED,tile_num(self_obj),elevation(self_obj)); // edit by killap
               set_map_var(Landmine_Var,MINE_DISARMED);
               if (source_obj == dude_obj) then
                   display_msg(message_str(SCRIPT_GENERIC,102));
               else
                   display_msg(obj_name(source_obj)+message_str(SCRIPT_GENERIC,105));
               if (local_var(LVAR_Gave_Exp) == 0) then begin
                   set_local_var(LVAR_Gave_Exp,1);
                   give_xp(TRAP_EXP);
               end
           end

           else if (is_critical(Traps_Roll)) then begin
               set_map_var(Landmine_Var,MINE_EXPLODED);
               if (source_obj == dude_obj) then
                  display_msg(message_str(SCRIPT_GENERIC,104));
               else
                  display_msg(obj_name(source_obj)+message_str(SCRIPT_GENERIC,107));
               //for some reason this code segment causes issues - killap
               /*if (tile_contains_obj_pid(tile_num(self_obj),elevation(self_obj),ART_TRAP_VISIBLE)) then begin
                   debug_msg("Destroying Visible Trap");
                   destroy_object(tile_contains_pid_obj(tile_num(self_obj),elevation(self_obj),ART_TRAP_VISIBLE));
                   debug_msg("Destroyed Visible Trap");
               end*/
               //Trap:=create_object_sid(ART_TRAP_EXPLODED,tile_num(self_obj),elevation(self_obj),CUR_SCRIPT);
               Trap:=create_object(ART_TRAP_EXPLODED,tile_num(self_obj),elevation(self_obj)); // edit by killap
               explosion(tile_num(self_obj),elevation(self_obj),random(MIN_DMG,MAX_DMG));
           end

           else begin
               if (source_obj == dude_obj) then
                   display_msg(message_str(SCRIPT_GENERIC,103));
               else
                   display_msg(obj_name(source_obj)+message_str(SCRIPT_GENERIC,106));
           end
      end
   end
end

procedure Check_Active(variable Mine_Var) begin
   variable Trap;

   if (map_var(Mine_Var) == MINE_ACTIVE) then begin
       if (tile_contains_obj_pid(tile_num(self_obj),elevation(self_obj),ART_TRAP_VISIBLE)) then begin
           debug_msg("Destroying Visible Trap");
           destroy_object(tile_contains_pid_obj(tile_num(self_obj),elevation(self_obj),ART_TRAP_VISIBLE));
           debug_msg("Destroyed Visible Trap");
       end
       Trap:=create_object(ART_TRAP_EXPLODED,tile_num(self_obj),elevation(self_obj));
       //if ((source_obj == dude_obj) and (has_trait(TRAIT_PERK,dude_obj,PERK_light_step_perk)) and (is_success(do_check(dude_obj,STAT_ag,-3)))) then
       if ((source_obj == dude_obj) and (has_trait(TRAIT_PERK,dude_obj,PERK_light_step_perk)) and (random(0,1) == 1)) then //added by killap
           debug_msg("Used Light Step.");
       else begin
          if (combat_is_initialized) then begin
             critter_dmg(source_obj, random(1, MAX_DMG), DAMAGE_TYPE);
             play_sfx("WHN1XXX1");
          end else begin
             explosion(tile_num(self_obj),elevation(self_obj),random(MIN_DMG,MAX_DMG));
          end
       end

       set_map_var(Mine_Var,MINE_EXPLODED);
   end
end

procedure Trap_Detection begin
   variable Range;
   variable Range_Mod;
   variable Detection_Roll;
   variable Trap;

   if (obj_type(source_obj) == 1) then begin

       Range:=get_critter_stat(source_obj,STAT_pe) + PERCEPTION_BONUS;
       Range_Mod:=Range - (tile_distance_objs(self_obj,source_obj)*DIST_BONUS);

       if ((tile_distance_objs(self_obj,source_obj) <= Range) and
           (local_var(LVAR_Trap_Found) == 0) and (local_var(LVAR_Trap_Exploded) == 0)) then begin

           if (Range_Mod > PERCEPTION_BONUS) then
               Range_Mod:= PERCEPTION_BONUS;

           Detection_Roll:=do_check(source_obj,STAT_pe,Range_Mod);

           if (is_success(Detection_Roll)) then begin

               set_local_var(LVAR_Trap_Found,1);
               reg_anim_clear(source_obj);
               Trap:=create_object_sid(ART_TRAP_VISIBLE,tile_num(self_obj),elevation(self_obj),CUR_SCRIPT);

               if (is_critical(Detection_Roll)) then begin
                   if (source_obj == dude_obj) then
                       display_msg(mstr(100));
                   else if ((proto_data(obj_pid(source_obj),cr_body_type) == CR_BODY_BIPED) and (obj_can_see_obj(source_obj,dude_obj))) then
                       display_msg(obj_name(source_obj)+mstr(101));
               end

               else begin
                   if (source_obj == dude_obj) then
                       display_msg(mstr(102));
                   else if ((proto_data(obj_pid(source_obj),cr_body_type) == CR_BODY_BIPED) and (obj_can_see_obj(source_obj,dude_obj))) then
                       display_msg(obj_name(source_obj)+mstr(103));
               end
           end
       end
   end
end